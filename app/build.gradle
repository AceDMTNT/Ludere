apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

ext.rom = new Properties()
file("${rootProject.projectDir}/config/rom.properties").withInputStream { ext.rom.load(it) }

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.draco.libretrowrapper.${rom.id}"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        manifestPlaceholders = [orientation:rom.orientation]
    }

    signingConfigs {
        release {
            storeFile file("${rootProject.projectDir}/libretrowrapper.jks")
            storePassword 'libretrowrapper'
            keyAlias 'key0'
            keyPassword 'libretrowrapper'
        }
    }

    buildTypes {
        all {
            resValue('string', 'config_name', rom.name)

            resValue('string', 'config_core', rom.core)

            resValue('color', 'config_foreground_color', rom.foregroundColor)
            resValue('color', 'config_background_color', rom.backgroundColor)

            resValue('string', 'config_variables', rom.variables)

            resValue('bool', 'config_gamepad_visible', rom.gamePadVisible)

            resValue('color', 'config_gamepad_icon_color', rom.gamePadIconColor)
            resValue('color', 'config_gamepad_button_color', rom.gamePadButtonColor)
            resValue('color', 'config_gamepad_pressed_color', rom.gamePadPressedColor)

            resValue('dimen', 'config_gamepad_spacing_vertical_portrait', rom.gamePadSpacingVerticalPortrait)
            resValue('dimen', 'config_gamepad_spacing_horizontal_portrait', rom.gamePadSpacingHorizontalPortrait)
            resValue('dimen', 'config_gamepad_spacing_vertical_landscape', rom.gamePadSpacingVerticalLandscape)
            resValue('dimen', 'config_gamepad_spacing_horizontal_landscape', rom.gamePadSpacingHorizontalLandscape)

            resValue('bool', 'config_gamepad_a', rom.gamePadA)
            resValue('bool', 'config_gamepad_b', rom.gamePadB)
            resValue('bool', 'config_gamepad_x', rom.gamePadX)
            resValue('bool', 'config_gamepad_y', rom.gamePadY)
            resValue('bool', 'config_gamepad_start', rom.gamePadStart)
            resValue('bool', 'config_gamepad_select', rom.gamePadSelect)
            resValue('bool', 'config_gamepad_l', rom.gamePadL)
            resValue('bool', 'config_gamepad_r', rom.gamePadR)
            resValue('bool', 'config_gamepad_save_state', rom.gamePadSaveState)
            resValue('bool', 'config_gamepad_load_state', rom.gamePadLoadState)
            resValue('bool', 'config_gamepad_fast_forward', rom.gamePadFastForward)
            resValue('bool', 'config_gamepad_mute', rom.gamePadMute)
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    preBuild.dependsOn 'cleanAssets'
    preBuild.dependsOn 'copyAssets'
}

task cleanAssets(type: Delete) {
    delete file('src/main/assets/').listFiles()
}

task copyAssets(type: Copy) {
    if (!rom.romPath.isEmpty()) {
        from "${rootProject.projectDir}/config/${rom.romPath}"
        into 'src/main/assets/'
        rename("${rom.romPath}", 'rom')
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.github.swordfish90:radialgamepad:0.1.0'
    implementation 'com.github.swordfish90:libretrodroid:0.4.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}